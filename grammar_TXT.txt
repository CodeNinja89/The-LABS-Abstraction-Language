// --- LARK Grammar for the LABS Language (Structured Version) ---

// The program is a strict sequence of four sections.
start: declarations_section initialisation_section preconditions_section postconditions_section program_section -> structured_program

// --- Section Definitions ---
declarations_section: "%% declarations" (struct_def | new_assignment | array_decl | var_decl | function_decl | const_decl)*
initialisation_section: "%% initialisation" assignment*
preconditions_section: "%% preconditions" (formula ";")*
postconditions_section: "%% postconditions" (formula ";")*
program_section: "%% program" statement*

// --- Re-usable Rules ---
array_decl: NAME ":" "array" "[" NAME "]"";" -> array_decl
struct_def: "struct" NAME "{" [field_decl ("," field_decl)*] "}" ";" -> struct_def
field_decl: NAME ":" type -> field
new_assignment: lvalue ":=" "new" NAME ";" -> new_assignment
function_decl: "function" NAME ":" function_sig "->" type ";" -> function_decl
function_sig: type ("," type)* -> function_sig    // argument types
type: "uint32" -> uint32_type
    | "uint8" -> uint8_type
    | "bool" -> bool_type
    | "array" "[" NAME "]" -> array_type
    | NAME -> user_type

var_decl: NAME ":" type ";" -> var_decl
const_decl: "const" NAME ":" type ";" -> const_decl

assignment: lvalue ":=" expr ";" -> assignment
assert: "assert" "(" formula ")" ";" -> assert_statement
skip: "skip" ";" -> skip

statement: assignment
         | assert
         | if_statement
         | while_loop
         | skip

block: "{" statement* "}"
if_statement: "if" "(" formula ")" block ["else" block] -> if_statement
while_loop: "while" "(" formula ")" ("invariant" formula)+ ("measure" expr)? block -> while_loop

lvalue: NAME ("." NAME)*

?formula: implication
?implication: disjunction | disjunction "=>" implication -> impliesf
?disjunction: conjunction | conjunction "||" disjunction -> orf
?conjunction: negation    | negation "&&" conjunction -> andf
?negation: "!" negation -> notf
         | quantifier
?quantifier: "forall" NAME ":" formula -> forallf
           | "exists" NAME ":" formula -> existsf
           | comparison

?comparison: expr "==" expr -> eqf
           | expr "!=" expr -> neqf
           | expr ">"  expr -> gtf
           | expr "<"  expr -> ltf
           | expr ">=" expr -> gef
           | expr "<=" expr -> lef
           | "true"        -> true_const
           | "false"       -> false_const
           | "(" formula ")"

?expr: bit_or

bit_or: bit_or "|" bit_xor      -> bit_or
      | bit_xor

bit_xor: bit_xor "^" bit_and    -> bit_xor
       | bit_and

bit_and: bit_and "&" shift      -> bit_and
       | shift

shift: shift "<<" term          -> shl
     | shift ">>" term          -> lshr      # logical right shift
     | add

add: add "+" term -> sum
     | add "-" term -> difference
     | term

?term: term "*" factor -> product
     | term "/" factor -> division
     | term "%" factor -> mod
     | factor

?factor: power // The power operator has been removed

?power: atom
?atom: NUMBER -> number
     | lvalue -> var
     | function_call
     | "-" atom -> neg_op
     | "~" atom -> bit_not
     | "(" expr ")"
function_call: NAME "(" [expr ("," expr)*] ")" -> function_call

// --- Terminal Definitions & Imports ---
%import common.CNAME -> NAME
%import common.SIGNED_NUMBER -> NUMBER
%import common.WS
%ignore WS
%import common.NEWLINE
%ignore NEWLINE
%import common.CPP_COMMENT
%ignore CPP_COMMENT